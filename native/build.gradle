// see https://github.com/TheInfiniteKind/appbundler

apply plugin: 'java'

task getXCrun(type: Exec) {
    commandLine "xcrun"
    args "--show-sdk-path"
    standardOutput = new ByteArrayOutputStream()
    ext.run = { return standardOutput.toString().trim() }
}

task compileNativeStarter(type: Exec) {

    dependsOn getXCrun
    def outputFile = new File( "${buildDir}", "JavaAppLauncher" )
    doFirst {

        def javahome = System.getProperty( "java.home" )
        def javahomeJRE = javahome;
        
        if ( file("${javahomeJRE}/../jre").exists() ) {
        	javahomeJRE += "/../jre";
        }
        
        def LIBJLI_DY_LIB="${javahomeJRE}/lib/libjli.dylib"
        if ( !file(LIBJLI_DY_LIB).exists() ) {
			LIBJLI_DY_LIB="${javahomeJRE}/lib/jli/libjli.dylib"
        }

        println "JAVA_HOME: ${javahome}";
        println "JAVA_HOME_JRE: ${javahomeJRE}";
        println "LIBJLI_DY_LIB: ${LIBJLI_DY_LIB}";

		args "-v"
        args "-arch"
        args "x86_64"
        args "-I"
        args "${javahome}/include"
        args "-I"
        args "${javahome}/include/darwin"
        args "-DLIBJLI_DYLIB=\"${LIBJLI_DY_LIB}\""
        args "-framework"
        args "Cocoa"
        args "-F"
        args "${javahome}/../.."
        args "-isysroot"
        args "${getXCrun.run()}"
        args "-mmacosx-version-min=10.7"
        args "-fobjc-exceptions"
        args "-std=c99"
        args "-o"
        args outputFile
        args "main.m"
    }

    doLast {
        outputFile.setExecutable( true, false)
        copy {
            from( outputFile )
            into('../src/com/oracle/appbundler')
        }
    }

    workingDir "."
    outputs.file( outputFile )

    commandLine "/usr/bin/gcc"
}

defaultTasks = ["compileNativeStarter", "clean"]